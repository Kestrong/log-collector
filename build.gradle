buildscript {
    repositories {
        maven {
            allowInsecureProtocol = true
            url MAVEN_REPOSITORY_ALIYUN
        }
        maven {
            allowInsecureProtocol = true
            url MAVEN_REPOSITORY_CENTRAL
        }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:3.2.2'
    }
}

plugins {
    id 'java'
}

allprojects {
    group 'com.xjbg'
    version '1.0.0-S3-SNAPSHOT'

    configurations.all {
        // 动态版本
        resolutionStrategy.cacheDynamicVersionsFor 0, 'minutes'
        // 变化模块
        resolutionStrategy.cacheChangingModulesFor 0, 'minutes'
    }
}

def versionMap = [
        "springCloudVersion": "2023.0.0",
        "springboot"        : "3.2.2",
        "nacos"             : "2.2.2.RELEASE",
        "es"                : "8.11.1"
]

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    repositories {
        maven {
            allowInsecureProtocol = true
            url MAVEN_REPOSITORY_ALIYUN
        }
        maven {
            allowInsecureProtocol = true
            url MAVEN_REPOSITORY_CENTRAL
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allJava
    }

    publishing {
        repositories {
            mavenLocal()
            maven {
                credentials {
                    username MAVEN_REPOSITORY_USER
                    password MAVEN_REPOSITORY_PWD
                }
                allowInsecureProtocol = true
                if(project.version.endsWith('-SNAPSHOT')) {
                    url MAVEN_REPOSITORY_SNAPSHOT
                } else {
                    url MAVEN_REPOSITORY_RELEASE
                }
            }
        }
        publications {
            maven(MavenPublication) {
                pom {
                    packaging 'jar'
                }
                groupId project.group
                artifactId project.name
                version project.version

                from components.java
                artifact sourcesJar

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }

    dependencyManagement {
        dependencies {
            dependency 'org.projectlombok:lombok:1.18.30'
            dependency group: 'junit', name: 'junit', version: '4.13'
            dependency 'org.openjdk.jol:jol-core:0.17'
            dependency 'jakarta.json:jakarta.json-api:2.1.1'
            dependency 'org.apache.commons:commons-lang3:3.9'
            dependency 'commons-collections:commons-collections:3.2'
            dependency 'mysql:mysql-connector-java:5.1.49'
            dependency "org.elasticsearch:elasticsearch:7.5.1"
            dependency "org.elasticsearch.client:elasticsearch-rest-client:$versionMap.es"
            dependency "org.elasticsearch.client:elasticsearch-rest-high-level-client:7.5.1"
            dependency "co.elastic.clients:elasticsearch-java:$versionMap.es"
            dependency 'io.github.openfeign:feign-httpclient:11.10'
            dependency 'org.apache.httpcomponents:httpclient:4.5.14'
            dependency 'org.springframework.cloud:spring-cloud-starter-netflix-hystrix:2.2.6.RELEASE'
            dependency "com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery:$versionMap.nacos"
            dependency "com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config:$versionMap.nacos"
        }
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$versionMap.springCloudVersion"
            mavenBom "org.springframework.boot:spring-boot-dependencies:$versionMap.springboot"
            mavenBom 'com.fasterxml.jackson:jackson-bom:2.14.2'
        }

    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testImplementation group: 'junit', name: 'junit'
    }
}
